# Maninka morphotactic patterns

# macro definitions
macro   @nasal-v@       <re>.*[aoeuiɔɛ]n</re>
macro   @nonnasal-v@    <re>.*[aoeuiɔɛ][^n]?</re>

# processing instructions
plan
for token:
stage 0 add sequential parse common_inflection
stage 0 add parallel parse v_inflection 
stage 0 apply lookup
return if parsed
stage 1 add sequential parse common_derivation
stage 1 add parallel parse v_derivation
stage 1 add parallel parse v_prefixes
stage 1 add parallel parse n_derivation
stage 1 add parallel parse vq_derivation
stage 1 apply lookup
return if parsed
stage 2 add parallel parse reduplication
stage 2 apply lookup
return if parsed
stage 3 add parallel decompose pos_composition
return if parsed

## INFLECTION

# nominal/common inflection
section common_inflection
# -` ART
pattern :n/adj/ptcp/n.prop: [ {|`}:: ] | :n/adj/ptcp/n.prop: [:n/adj/ptcp/n.prop: :mrph:ART]
# -lù, -nù, -lú PL
pattern :n/adj/num/dtm/prn/ptcp/n.prop: [ {@nonasal-v@|lu}:: ] | :n/adj/num/dtm/prn/ptcp/n.prop: [:n/adj/num/dtm/prn/ptcp/n.prop: :mrph:PL]
pattern :n/adj/num/dtm/prn/ptcp/n.prop: [ {@nasal-v@|nu}:: ] | :n/adj/num/dtm/prn/ptcp/n.prop: [:n/adj/num/dtm/prn/ptcp/n.prop: :mrph:PL]


section v_inflection
# verbal inflection
# -la/-na INF
pattern :v: [ {@nonnasal-v@|la}:: ] | :v: [:v: :mrph:INF]
pattern :v: [ {@nasal-v@|na}:: ] | :v: [:v: :mrph:INF]
# -da/-ra/-na AOR.INTR
pattern :v: [ {|da}:: ] | :v: [:v: :mrph:AOR.INTR]
pattern :v: [ {@nonnasal-v@|ra}:: ] | :v: [:v: :mrph:AOR.INTR]
pattern :v: [ {@nasal-v@|na}:: ] | :v: [:v: :mrph:AOR.INTR]
# -san IMMED
pattern :v: [ {|san}:: ] | :v: [:v: :mrph:IMMED] 
# participles
# -nin/-nɛn PTCP.RES
pattern :v/ptcp: [ {|nin}:: ] | :ptcp: [:v: :mrph:PTCP.RES]
pattern :v/ptcp: [ {|nɛn}:: ] | :ptcp: [:v: :mrph:PTCP.RES]
# -ta PTCP.POT
pattern :v/ptcp: [ {|ta}:: ] | :ptcp: [:v: :mrph:PTCP.POT]
# -tɔ PTCP.PROG
pattern :v/ptcp: [ {|tɔ}:: ] | :ptcp: [:v: :mrph:PTCP.PROG]
# -bali PTCP.NEG
pattern :v/ptcp: [ {|bali}:: ] | :ptcp: [:v: :mrph:PTCP.NEG]

## DERIVATION

section common_derivation
# -ya/-ɲa ABSTR
pattern :n/v: [ {|ya}:: ] | :n: [:n/adj/ptcp: :mrph:ABSTR]
pattern :n/v: [ {|ɲa}:: ] | :n: [:n/adj/ptcp: :mrph:ABSTR]
# -nin DIM
pattern :n/adj/ptcp: [ {|nin}:: ] | :n/adj/ptcp: [ :n/adj/ptcp: :mrph:DIM ] 
# -ba AUGM
pattern :n/adj/ptcp: [ {|ba}:: ] | :n/adj/ptcp: [ :n/adj/ptcp: :mrph:AUG ]
# -nte AG.EX
pattern :n: [ {|nte}:: ] | :n: [ :n/adj/v: :mrph:AG.EX ] 
# ‑ɲɔɔn RECP
pattern :n: [ {|ɲɔɔn}:: ] | :n: [ :v/n: :mrph:RECP]


# verbal derivation
section v_derivation
# -baa AG.OCC
pattern :n/adj: [ {|baa}:: ] | :n/adj: [ :v: :mrph:AG.OCC ]
# -la/-na AG.PRM
pattern :n: [ {@nonnasal-v@|la}:: ] | :n: [ :v: :mrph:AG.PRM ] 
pattern :n: [ {@nasal-v@|la}:: ] | :n: [ :v: :mrph:AG.PRM ] 
# -lan/-nan/-ran INSTR
pattern :n: [ {@nonnasal-v@|lan}:: ] | :n: [ :v: :mrph:INSTR]
pattern :n: [ {@nasal-v@|nan}:: ] | :n: [ :v: :mrph:INSTR]
# -li/-nin NMLZ
pattern :n: [ {@nonnasal-syl@|li}:: ] | :n: [ :v: :mrph:NMLZ]
pattern :n: [ {@nasal-syl@|nin}:: ] | :n: [ :v: :mrph:NMLZ]

section v_prefixes
# lá-/ná- CAUS
pattern :v: [ {la|}:: ] | :v: [ :mrph:CAUS :v: ] 
pattern :v: [ {na|}:: ] | :v: [ :mrph:CAUS :v: ] 
# mà- SUPER
pattern :v: [ {ma|}:: ] | :v: [ :mrph:SUPER :v: ]
# dɔ́ ~ rɔ́-/nɔ́ IN
pattern :v: [ {dɔ|}:: ] | :v: [ :mrph:IN :v: ] 
pattern :v: [ {rɔ|}:: ] | :v: [ :mrph:IN :v: ] 
pattern :v: [ {nɔ|}:: ] | :v: [ :mrph:IN :v: ] 

section n_derivation
# -laka LOC.GENT
pattern :n/n.prop: [ {|la|ka}:: ] | :n/n.prop: [ :n/n.prop: :mrph:LOC :mrph:GENT ] 
# -ka  GENT
pattern :n/n.prop: [ {|ka}:: ] | :n/n.prop: [ :n/n.prop: :mrph:GENT ]
# -la/-na LOC
pattern :n/n.prop: [ {@nonnasal-v@|la}:: ] | :n/n.prop: [:n/n.prop: :mrph:LOC]
pattern :n/n.prop: [ {@nasal-v@|na}:: ] | :n/n.prop: [:n/n.prop: :mrph:LOC]
# -ntan PRIV
pattern :adj/n: [ {|ntan}:: ] | :adj/n: [ :n: :mrph:PRIV]
# -ma COM
pattern :n: [ {|ma}:: ] | :adj/n: [ :n: :mrph:COM]
# -nan ORD
pattern :num: [ {|nan}:: ] | :adj: [ :num: :mrph:ORD ]
# -ta FOC.ADJ
pattern :adj: [ {|ta}:: ] | :adj: [ :adj: :mrph:FOC.ADJ ]
# -tɔ ST
pattern :n/adj: [ {|tɔ}:: ] | :n/adj: [ :n: :mrph:ST ] 


# vq derivation
section vq_derivation
# -man ADJ
pattern :vq: [ {|man}:: ] | :adj: [ :vq: :mrph:ADJ ]
# -ya/-ɲa DEQU
pattern :vq: [ {@nonnasal-v@|ya}:: ] | :n/v: [ :vq: :mrph:DEQU]
pattern :vq: [ {@nasal-v@|ɲa}:: ] | :n/v: [ :vq: :mrph:DEQU]

## reduplication
section reduplication
pattern :v: [ {<re>(?P<stem>.+)</re>|<re>(?P=stem)</re>}:: ] | :v: [ :v: :v: ]
pattern :adj: [ {<re>(?P<stem>.+)</re>|<re>(?P=stem)</re>}:: ] | :adj: [ :adj: :adj: ]
pattern :num: [ {<re>(?P<stem>.+)</re>|<re>(?P=stem)</re>}:: ] | :num: [ :num: :num: ]
pattern :v: [ {<re>(?P<stem>.+)</re>|-|<re>(?P=stem)</re>}:: ] | :v: [ :v: :: :v: ]
pattern :adj: [ {<re>(?P<stem>.+)</re>|-|<re>(?P=stem)</re>}:: ] | :adj: [ :adj: :: :adj: ]
pattern :num: [ {<re>(?P<stem>.+)</re>|-|<re>(?P=stem)</re>}:: ] | :num: [ :num: :: :num: ]
pattern :v: [ {<re>(?P<stem>.+)</re>|<re>(?P=stem)</re>|<re>(?P=stem)</re>}:: ] | :v: [ :v: :v: :v: ]
pattern :adj: [ {<re>(?P<stem>.+)</re>|<re>(?P=stem)</re>|<re>(?P=stem)</re>}:: ] | :adj: [ :adj: :adj: :adj: ]

## composition
# general part-of-speech composition patterns
#
# two-word composites
section pos_composition
# v/n + n = n
pattern :n: [ :n/v: :n: ] | :n: [ :n/v: :n: ]
# n + v = n/v
pattern :n/v: [ :n: :v: ] | :n/v: [ :n: :v: ]
# n + adj/num = n
pattern :n: [ :n: :adj/num: ] | :n: [ :n: :adj/num: ]
# dtm + v = n
pattern :n: [ :dtm: :v: ] | :n: [ :dtm: :v: ]
# three-word composites
# n + adj/pp + n = n
pattern :n: [ :n: :adj/pp: :n: ] | :n: [ :n: :adj/pp: :n: ]
# n + pp + n + n = n
pattern :n: [ :n: :pp: :n: :n: ] | :n: [ :n: :pp: :n: :n: ]
# dtm/n + v + n = n
pattern :n: [ :dtm/n: :v: :n: ] | :n: [ :dtm/n: :v: :n: ]
# n + pp + v = n/v
pattern :n/v: [ :n: :pp: :v: ] | :n/v: [ :n: :pp: :v: ]


